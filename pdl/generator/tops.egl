[*"tops" stands for "template operations"*]
[%import "../pdl.eol";%]
[%
@template
operation Collection<Deliverable> toDeliverablesTable() {
%]
| Title | Due |
| --- | --- |
[%for (deliverable in self){%]
[%if (deliverable.delivered){%]
|[[%=deliverable.getId()%] [%=deliverable.title%]](/deliverables/[%=deliverable.getId()%].pdf) | M[%=deliverable.due%] |
[%}else{%]
|[%=deliverable.getId()%] [%=deliverable.title%] | M[%=deliverable.due%] |
[%}%]
[%}%]
[%    
}
%]

[%
@template
operation Collection<Task> toTasksTable() {%]
| Title | Start | End |
| --- | --- | --- |
[%for (task in self){%]
|[[%=task.getId()%] [%=task.title%]](/wp/[%=task.eContainer().getIndex()%]/task/[%=task.getIndex()%]) | M[%=task.start%] | M[%=task.end%] |
[%}%]
[%    
}
%]

[%
@template
operation Collection<Partner> toPartnersList() {%]
[%for (partner in self){%]
- [[%=partner.title%]](/partner/[%=partner.name.toLowerCase()%])[%if (loopCount == 1){%] (lead) [%}%] 
[%}%]
[%    
}
%]
